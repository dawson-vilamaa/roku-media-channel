<?xml version = "1.0" encoding = "utf-8" ?>

<component name = "VideoListExample" extends = "Scene" >

  <script type = "text/brightscript" >

    <![CDATA[

    sub init()
      m.top.backgroundURI = "pkg:/images/background.jpg"

      m.videolist = m.top.findNode("videoLabelList")
      m.videoposter = m.top.findNode("videoPoster")
      m.video = m.top.findNode("exampleVideo")

      m.video.observeField("state", "controlvideoplay")

      m.readVideoContentTask = createObject("roSGNode", "ContentReader")
      m.readVideoContentTask.observeField("content", "showvideolist")
      m.readVideoContentTask.contenturi = "http://10.0.0.242:6969/media.xml"
      m.readVideoContentTask.control = "RUN" 

      m.videolist.observeField("itemFocused", "setvideo")
      m.videolist.observeField("itemSelected", "playvideo")
    end sub

    sub showvideolist()
      m.videolist.content = m.readVideoContentTask.content
      m.videolist.setFocus(true)
    end sub

    sub setvideo()
      videocontent = m.videolist.content.getChild(m.videolist.itemFocused)
      m.videoposter.uri = videocontent.hdposterurl
      m.video.content = videocontent
    end sub

    sub playvideo()
      m.video.control = "play"
      m.video.visible = true
      m.video.setFocus(true)
    end sub

    sub controlvideoplay()
      if (m.video.state = "finished") 
        m.video.control = "stop"
        m.videolist.setFocus(true)
        m.video.visible = false
      end if
    end sub

    function onKeyEvent(key as String, press as Boolean) as Boolean
      if press then
        if key = "back"
          if (m.video.state = "playing")
            m.video.control = "stop"
            m.videolist.setFocus(true)
            m.video.visible = false

            return true
          end if
        end if
      end if

      return false
    end function

    ]]>

  </script>

  <children>

    <LabelList 
      id = "videoLabelList" 
      translation = "[ 140, 100 ]" 
      itemSize = "[ 350, 60 ]" />

    <Poster 
        id = "videoPoster" 
        translation = "[ 710, 65 ]" 
        width = "375" 
        height = "600" />

    <Video 
      id = "exampleVideo" 
      visible = "false" />

  </children>

</component>
